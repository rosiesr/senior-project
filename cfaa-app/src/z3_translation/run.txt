(set-option :produce-unsat-cores true)

(declare-const KnowinglyAccessesComputerWithoutAuthorization Bool) ;need input
(declare-const KnowinglyAccessesComputerExceedingAuthorization Bool) ;need input
(declare-const ObtainsRestrictedInformation Bool) ;internal
(declare-const InformationDeterminedToRequireProtection Bool) ;need input
(declare-const IsProtectedComputer Bool) ; internal

(declare-const InformationCanBeUsedToInjureUS Bool) ;need input
(declare-const InformationCanBeUsedToAdvantageForeignNation Bool) ;need input
(declare-const CommunicatesInformationToUnauthorizedPerson Bool);need input
(declare-const FailedToDeliverToUSEntity Bool);need input

(declare-const ObtainsRestrictedInformationSection2 Bool) ;internal

(declare-const FinanciallyRestrictedInformation Bool) ; internal
(declare-const ContainsFinancialRecordofFinancialInstitution Bool) ;need input
(declare-const ContainedInConsumerReportingAgency Bool) ;need input
(declare-const InformationFromProtectedComputer Bool) ;need input
(declare-const InformationFromDepartmentOrAgencyOfUS Bool) ;need input

(declare-const IntentionallyAccessesComputerWithoutAuthorization Bool) ;need input
(declare-const NonpublicComputerOfUSDepartmentOrAgency Bool) ;need input

(declare-const ComputerExclusiveGovOrAffectedGov Bool) ;internal
(declare-const ComputerExclusivelyForGovernmentUse Bool) ;need input
(declare-const ConductAffectsGovernmentUse Bool) ;need input

(declare-const IntentToDefraud Bool) ;need input
(declare-const ObtainsAnythingOfValue Bool) ;need input
(declare-const ObjectOfFraudAndThingObtainedOnlyUseOfComputer Bool) ;need input
(declare-const ValueOfUseOfComputerNotMoreThan5000 Bool) ;need input
(declare-const SignificantObjectOfFraud Bool) ;internal

(declare-const KnowinglyCausesTransmissionOfCode Bool) ;need input
(declare-const TransmissionCausesDamageWithoutAuthOfProtectedComputer Bool) ;need input
(declare-const IntentionallyAccessesProtectedComputerWithoutAuthorization Bool) ;need input
(declare-const ConductResultsInDamageAndLoss Bool) ;need input
(declare-const ConductResultsInRecklessDamage Bool) ;need input
(declare-const IntentionallyAccessAndDamage Bool) ; internal

(declare-const KnowinglyAndWithIntentToDefraudTrafficsInPassword Bool) ; need input TO DO CHECK THIS
(declare-const TraffickingnAffectsInterstateOrForeignCommerce Bool) ; internal 
(declare-const ComputerUsedByOrForGovernment Bool ) ; need input ?

(declare-const IntentToExtortTransmitsInInterstateCommunication Bool ) ; need input ?
(declare-const ThreatToDamageProtectedComputer Bool ) ; need input ?
(declare-const ThreatToObtainInfoFromProtectedComputerWithoutAuth Bool ) ; need input 
(declare-const ImpairConfidentialityOfInfoObtainedFromProtectedComputer Bool ) ; need input 
(declare-const DemandForMoneyInRelationToDamageProtectedComputer Bool ) ; need input 
(declare-const ContainsThreatOrExtotion Bool ) ; internal






(declare-const NotCompliant Bool) ;internal

(assert (=> (and IntentionallyAccessesComputerWithoutAuthorization IsProtectedComputer ) IntentionallyAccessesProtectedComputerWithoutAuthorization))

;Section 1
(assert (=> (and (or KnowinglyAccessesComputerWithoutAuthorization KnowinglyAccessesComputerExceedingAuthorization) ObtainsRestrictedInformation) NotCompliant))
;define obtaining restricted information
(assert (=> (and InformationDeterminedToRequireProtection (or InformationCanBeUsedToInjureUS InformationCanBeUsedToAdvantageForeignNation CommunicatesInformationToUnauthorizedPerson FailedToDeliverToUSEntity )) ObtainsRestrictedInformation))

;Section 2
(assert (=> (and (or KnowinglyAccessesComputerWithoutAuthorization KnowinglyAccessesComputerExceedingAuthorization) ObtainsRestrictedInformationSection2) NotCompliant))
(assert (=> (or ContainsFinancialRecordofFinancialInstitution ContainedInConsumerReportingAgency) FinanciallyRestrictedInformation ))
(assert (=> (or FinanciallyRestrictedInformation InformationFromDepartmentOrAgencyOfUS InformationFromProtectedComputer ) ObtainsRestrictedInformationSection2 ))

;Section 3
(assert (=> (or ComputerExclusivelyForGovernmentUse (and (not ComputerExclusivelyForGovernmentUse) ConductAffectsGovernmentUse )) ComputerExclusiveGovOrAffectedGov ))
(assert (=> (and IntentionallyAccessesComputerWithoutAuthorization ComputerExclusiveGovOrAffectedGov NonpublicComputerOfUSDepartmentOrAgency ) NotCompliant ))

;Section 4
(assert (=> (or ObtainsAnythingOfValue (not (and ObjectOfFraudAndThingObtainedOnlyUseOfComputer ValueOfUseOfComputerNotMoreThan5000 ))) SignificantObjectOfFraud ))
(assert (=> (and (and (or KnowinglyAccessesComputerWithoutAuthorization KnowinglyAccessesComputerExceedingAuthorization) IntentToDefraud) SignificantObjectOfFraud) NotCompliant))

;Section 5
(assert (=> (and IntentionallyAccessesProtectedComputerWithoutAuthorization (or ConductResultsInDamageAndLoss ConductResultsInRecklessDamage)) IntentionallyAccessAndDamage ))
(assert (=> (or IntentionallyAccessAndDamage (and KnowinglyCausesTransmissionOfCode TransmissionCausesDamageWithoutAuthOfProtectedComputer )) NotCompliant))

;Section 6
(assert (=> (and KnowinglyAndWithIntentToDefraudTrafficsInPassword (or TraffickingnAffectsInterstateOrForeignCommerce ComputerUsedByOrForGovernment )) NotCompliant))

;Section 7
(assert (=> (or ThreatToDamageProtectedComputer ThreatToObtainInfoFromProtectedComputerWithoutAuth DemandForMoneyInRelationToDamageProtectedComputer) ContainsThreatOrExtotion))
(assert (=> (and IntentToExtortTransmitsInInterstateCommunication ContainsThreatOrExtotion) NotCompliant))


(assert (! KnowinglyAccessesComputerWithoutAuthorization :named a1))
(assert (! (not InformationDeterminedToRequireProtection) :named a3))
(assert (! (not InformationCanBeUsedToInjureUS) :named a4))
(assert (! (not InformationCanBeUsedToAdvantageForeignNation) :named a5))
(assert (! (not CommunicatesInformationToUnauthorizedPerson) :named a6))
(assert (! (not FailedToDeliverToUSEntity) :named a7))
(assert (! ContainsFinancialRecordofFinancialInstitution :named a8))
(assert (! (not ContainedInConsumerReportingAgency) :named a9))
(assert (! (not InformationFromDepartmentOrAgencyOfUS) :named a11))
(assert (! IntentionallyAccessesComputerWithoutAuthorization :named a12))
(assert (! (not IntentToDefraud) :named a16))
(assert (! (not NotCompliant) :named notComp))
(check-sat)
